(* root-task EBNF grammar *)

root-task = <LP> symbol <'.'> symbol argval* <RP>
symbol = !boolean #'[A-Za-z][A-Za-z0-9-]*'
boolean = ( TRUE | FALSE )
argval = ( boolean | string | number | safe-keyword )
string = <'"'>  #'[^"]*' <'"'>
number = ( float | integer )
float = #'[-]?[0-9]+\.[0-9]+'
integer = #'[-]?[0-9]+'
natural = #'[0-9]+'
safe-keyword = !reserved-keyword keyword
reserved-keyword = ( <LABEL> | <BOUNDS> | <COST> | <REWARD> | <CONTROLLABLE> )
keyword = <':'> #'[A-Za-z0-9:*-.]+'

BOUNDS = <':bounds'>
CONTROLLABLE = <':controllable'>
COST = <':cost'>
FALSE = <'false'>
LABEL = <':label'>
REWARD = <':reward'>
TRUE = <'true'>

LP = <'('>
RP = <')'>
